plugins {
    // use older kotlin version compatible with Gradle
    alias(libs.plugins.kotlin.jvm)
    id 'java-gradle-plugin'
    alias(libs.plugins.kotlin.dsl)
    alias(libs.plugins.spotless)
}

group = "nl.littlerobots.djuptodate.build"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    jvmToolchain(17)
}

gradlePlugin {
    plugins {
        register("basicAndroidConventions") {
            id = "nl.littlerobots.android-basic-conventions"
            implementationClass = "nl.littlerobots.abc.plugin.AndroidBasicConventionsPlugin"
        }
    }
}

spotless {
    kotlin {
        target("**/*.kt")
        ktfmt("0.44")
        licenseHeaderFile file('spotless/license.txt')
    }

    groovyGradle {
        target "**/*.gradle"
        trimTrailingWhitespace()
        endWithNewline()
    }
}

dependencies {
    compileOnly libs.android.gradle.api
    compileOnly libs.kotlin.gradle.plugin
}
